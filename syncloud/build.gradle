apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 21060
        versionName "21.06"
        multiDexEnabled true
    }

    useLibrary 'org.apache.http.legacy'
    flavorDimensions "full"
    productFlavors {
        prod {
            applicationId "org.syncloud.android"
        }
    }

    signingConfigs {
        release {
            storeFile file('syncloud.keystore')
            storePassword ''
            keyAlias ''
            keyPassword ''
        }
    }

    buildTypes {
        release {
            debuggable false
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'log4j.properties'
        exclude 'about.html'
        exclude 'META-INF/beans.xml'
    }

    allprojects {
        afterEvaluate { project ->
            def env = System.getenv()
            if (env.containsKey('KEY_STORE')) {
                def storeFile = new File(env['ANDROID_STORE_FILE'])
                storeFile.append(env['KEY_STORE'].decodeBase64())

                android.signingConfigs['release'].storeFile = storeFile
                android.signingConfigs['release'].storePassword = env['ANDROID_STORE_PASSWORD']
                android.signingConfigs['release'].keyAlias = env['ANDROID_KEY_ALIAS']
                android.signingConfigs['release'].keyPassword = env['ANDROID_KEY_PASSWORD']
            } else {
                logger.warn('KEY_STORE is not available, will not be able to publish to Google Play')
            }
        }
    }

}


dependencies {
    implementation 'log4j:log4j:1.2.17'
    implementation 'ch.acra:acra-mail:5.1.3'
    implementation 'ch.acra:acra-dialog:5.1.3'
    implementation 'de.mindpipe.android:android-logging-log4j:1.0.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.3.3'
    implementation 'org.apache.commons:commons-lang3:3.3.2'
    implementation 'org.apache.httpcomponents:httpclient-android:4.3.3'
    implementation 'com.google.guava:guava:24.1-jre'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.lsjwzh:materialloadingprogressbar:0.5.8-RELEASE'
    testImplementation 'junit:junit:4.10'
    testImplementation 'org.mockito:mockito-all:1.9.5'
    implementation "androidx.core:core-ktx:+"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.30-M1"
}